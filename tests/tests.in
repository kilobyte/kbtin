### #showme
<<<
#showme abc def ghi
#showme { blah}
#showme ąęść
#var x foo
#showme $x
>>>
abc def ghi
 blah
ąęść
foo
===


### no profiling
<<<
#rem Profiling should be disabled in release versions.
#profile
>>>
#UNKNOWN TINTIN-COMMAND: [#profile]
===


### #reverse
<<<
#var x blahąęśćabc
#reverse x $x
#showme $x
>>>
cbaćśęąhalb
===


### #postpad
<<<
#postpad tmp 5 abcdefgh;#showme {$tmp|}
#postpad tmp 5 abćdęfgh;#showme {$tmp|}
#postpad tmp 5 ab;#showme {$tmp|}
#postpad tmp 5 ać;#showme {$tmp|}
#var x foo
#postpad tmp 5 {a$x};#showme {$tmp|}
#postpad tmp 5 a ć;#showme {$tmp|}
>>>
abcde|
abćdę|
ab   |
ać   |
afoo |
a ć  |
===

### #prepad
<<<
#prepad tmp 5 abcdefgh;#showme {$tmp|}
#prepad tmp 5 abćdęfgh;#showme {$tmp|}
#prepad tmp 5 ab;#showme {$tmp|}
#prepad tmp 5 ać;#showme {$tmp|}
#var x foo
#prepad tmp 5 {a$x};#showme {$tmp|}
#prepad tmp 5 a ć;#showme {$tmp|}
>>>
defgh|
dęfgh|
   ab|
   ać|
 afoo|
  a ć|
===

### #substr
<<<
#substr tmp 3 abcde;#showme {$tmp}
#substr tmp 2,3 abcde;#showme {$tmp}
#substr tmp 4,1000 abcde;#showme {$tmp}
#substr tmp 2,4 ąbćdef;#showme {$tmp}
>>>
c
bc
de
bćd
===

### late var substitution
<<<
#var x A1
#alias foo {#alias bar {#showme $x}}
foo
bar
#var x B1
bar
#var x C1
foo
#var x D1
bar
#var x A2
#alias foo {#alias bar {#showme $$x}}
foo
bar
#var x B2
bar
#var x C2
foo
#var x D2
bar
#var x A3
#alias foo {#alias bar {#showme $$$x}}
foo
bar
#var x B3
bar
#var x C3
foo
#var x D3
bar
>>>
A1
B1
D1
A2
A2
C2
A3
A3
A3
===

### #system
<<<
#var x abc
#sys echo '$x'
#sys {echo '$x'}
>>>
abc
abc
===

### #shell
<<<
#shell echo 'abc'
>>>
abc
===

### #run
<<<
#hook close {#showme FIN;#end}
#run a echo blah
>>>
blah
FIN
===

### #run and 16bit Unicode
<<<
#hook close {#showme FIN;#end}
#run a echo abcąęśćdefабцдефгхийאבגדァアィイ
>>>
abcąęśćdefабцдефгхийאבגדァアィイ
FIN
===

### #goto
<<<
#route a b {#showme A>B}
#route b c {#showme B>C}
#goto A B;#showme -=-
#goto a b;#showme -=-
#goto a c;#showme -=-
#route a c {#showme A>C} 5
#goto a c;#showme -=-
#route a c {#showme A>C} 21
#goto a c;#showme -=-
#goto a {};#showme -=-
#goto {} b;#showme -=-
#goto {} {};#showme -=-
a>b;#showme -=-
>c;#showme -=-
#showme ($loc)
>>>
#Location not found: [A]
-=-
A>B
-=-
A>B
B>C
-=-
A>C
-=-
A>B
B>C
-=-
#SYNTAX: #goto <from> <to>
-=-
#SYNTAX: #goto <from> <to>
-=-
#SYNTAX: #goto <from> <to>
-=-
A>B
-=-
B>C
-=-
(c)
===

### #end w/o sessions
<<<
#end
>>>
===

### #end with a session
<<<
#run a cat
#end
>>>

#SESSION 'a' DIED.
===

### action priorities
<<<
#act {aaa%0} {#showme act: a} {a}
#act {aaa%1} {#showme act: 0} {0}
#act {aaa%2} {#showme act: 0.09} {0.09}
#act {aaa%3} {#showme act: 0.1} {0.1}
#act {aaa%4} {#showme act: 0.10} {0.10}
#act {aaa%5} {#showme act: 0.19} {0.19}
#act {aaa%6} {#showme act: 0a.1} {0a.1}
#act {aaa%7} {#showme act: a06z} {a06z}
#act {aaa%8} {#showme act: a6x} {a6x}
#act {aaa%9} {#showme act: 0.2} {0.2}
#doact aaa
>>>
act: 0
act: 0.1
act: 0.2
act: 0.09
act: 0.10
act: 0.19
act: 0a.1
act: a
act: a6x
act: a06z
===

### actions, promptactions
<<<
#hook close {#showme FIN;#end}
#act {foo} {#showme <foo>}
#promptact {fo} {#showme <fo...>}
#run a {sh -c "echo -n 'fo';sleep 2;echo 'o'"}
>>>
<fo...>
<fo...>
<foo>
foo
FIN
===
